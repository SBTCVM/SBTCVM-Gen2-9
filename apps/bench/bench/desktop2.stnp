


#gsub draw_desktop

var xbuff=0
var ybuff=0
var actflg=@1
var btnbuff=0
var cl_y=0
var cl_y2=0

var isbackup=0

#done via (pixy*icon_height+5)
var i_yspace=@32

var i_ysize=@27
var i_ygap=@5
var i_start_y=@-90


if isbackup,@0 begin
	val @1
	set isbackup
	##moved to sndkern
	#gsub common.tune
	!sklib.sk_startup()
	gsub draw_desktop
	drawcopy @12
	drawcopy @-1
end
val @1
set actflg

#local macro functions for making y position checks automatic.
def cl_y_init {get i_start_y / set cl_y}
def get_cl_y2 {add cl_y,i_ysize / set cl_y2}
def inc_cl_y {add cl_y,i_yspace / set cl_y}



loop
	
	if actflg,@1 begin
		val @0
		set actflg
		drawblit @12
		drawcopy @-1
	end
	
	{val 0 / set btnbuff}
	until btnbuff,@1
	
		{asm ioread1;>mouse.button / set btnbuff}
		gsub common.mouse_cursor
	
	end
	
	##### Main screen icon click parser #####
	
	{asm ioread1;>mouse.lockx / set xbuff}
	{asm ioread1;>mouse.locky / set ybuff}
	
	
	!cl_y_init()
	
	
	!get_cl_y2()
		ifrange cl_y,cl_y2,ybuff begin
		
			ifrange @-120,@-93,xbuff gsub info
		
		end
	!inc_cl_y()
	
	!get_cl_y2()
		ifrange cl_y,cl_y2,ybuff begin
		
			ifrange @-120,@-93,xbuff gsub dos
		
		end
	!inc_cl_y()
	
	!get_cl_y2()
		ifrange cl_y,cl_y2,ybuff begin
		
			ifrange @-120,@-93,xbuff gsub credits
		
		end
	!inc_cl_y()
end


label info
	gsub backup_self
	!b_lib.app_run(info.app)
return

label dos
	gsub backup_self
	!b_lib.app_run(dos.app)
return

label credits
	gsub backup_self
	!b_lib.app_run(credits.app)
return


label backup_self
	vdi membak 2 desktop.back.tri
return

label draw_desktop
	bprinttail1 DESKTOP
	gsub ui.base_draw
	
	val @3
	set pl.pixx
	set pl.pixy
	
	val @-120
	set pl.posx
	get i_start_y
	set pl.posy
	
	
	#info
		
		asm setreg1;>ICON_info
		set pl.offset
		gsub pl.decode
		{add pl.posy,i_yspace / set pl.posy}
	
	#dos
		asm setreg1;>ICON_dos
		set pl.offset
		gsub pl.decode
		{add pl.posy,i_yspace / set pl.posy}
	
	#credits
		asm setreg1;>ICON_credits
		set pl.offset
		gsub pl.decode
		{add pl.posy,i_yspace / set pl.posy}
return




include bench/lib/common as common
include bench/lib/ui as ui
include plrle_noalpha as pl
include bench/lib/sndkern_lib as sklib
include bench/lib/bench_lib as b_lib
asm include;bench/media/info.tas0;ICON_info
asm include;bench/media/dos.tas0;ICON_dos
asm include;bench/media/credits_icn.tas0;ICON_credits