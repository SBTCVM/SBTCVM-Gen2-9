#guess that trit v2.0
#a game originally written for SBTCVM Mark 2, by Thomas Leathers
#rewritten for SBTCVM Gen2-9, by Thomas Leathers
var score=0
var count=0
var countinc=+
var guess=0

var wrong=+
var right=++

var randstart=-
var randend=+
var randbuff=0

var chval=0

#keymapping
var poschar=:=
var poschar2=:+
var negchar=:-
var negchar2=:_
var gndchar=:0
var gndchar2=:)

var quitchar=:q
var quitchar2=:Q

var helpchar=:h
var helpchar2=:H

var aboutchar=:a
var aboutchar2=:A


prline Hey everyone and welcome back to:
prline GUESS!.. THAT!.. TRIT!

gsub printscore

label main
getchar chval

##guess entry
if poschar,chval gsub pos
if poschar2,chval gsub pos

if negchar,chval gsub neg
if negchar2,chval gsub neg

if gndchar,chval gsub gnd
if gndchar2,chval gsub gnd

##Other options.

if helpchar,chval gsub help
if helpchar2,chval gsub help

if aboutchar,chval gsub about
if aboutchar2,chval gsub about

if quitchar,chval gsub quit
if quitchar2,chval gsub quit

goto main


#####Positive guess subroutine
label pos
val +
set guess
gsub doscore
return


#####Negative guess subroutine
label neg
val -
set guess
gsub doscore
return


#####Ground guess subroutine
label gnd
val 0
set guess
gsub doscore
return

#####score calculator

label doscore
#get random number
rrange randstart,randend
set randbuff
#check if correct, if so jump to scoreadd section, else do score-sub section.
if randbuff,guess goto scoreadd
##wrong answer score code
sub score,wrong
set score
add count,countinc
set count
prline wrong.
gsub printscore
return

##correct answer score code
label scoreadd
add score,right
set score
add count,countinc
set count
prline Correct!
gsub printscore
return


#####score printer:
label printscore
print your score: 
dumpd score
newline
print out of    : 
dumpd count
newline
newline
prline enter your guess: (press 'h' for help.)
return

#####exit routine
label quit
newline
prline ----------------
newline
prline Final score:
print your score: 
dumpd score
newline
print out of    : 
dumpd count
newline
newline
prline Press any key to exit.
keyprompt
prline Goodbye!
stop

#####-----about subroutine-----
label about


prline --ABOUT--
prline Guess That Trit v2.0
prline Part of SBTCVM Gen2-9
prline 
prline Copyright (c) 2017-2018 Thomas Leathers & Contributors
prline 
prline Guess That Trit is free software: you can redistribute it and/or modify
prline it under the terms of the GNU General Public License as published by
prline the Free Software Foundation, either version 3 of the License, or
prline (at your option) any later version.
prline 
prline Guess That Trit is distributed in the hope that it will be useful,
prline but WITHOUT ANY WARRANTY; without even the implied warranty of
prline MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
prline GNU General Public License for more details.
prline 
prline You should have received a copy of the GNU General Public License
prline along with Guess That Trit. If not, see <http://www.gnu.org/licenses/>
prline 
return

#####-----help subroutine-----
label help
prline --RULES--
prline As you repeatedly guess randomly chosen trits:
prline Your score raises by 3 points on a right guess.
prline Your score lowers by 1 point for each wrong guess.
prline Both your score and your total number of guesses
prline is tracked.  Happy Guessing!
prline 
prline --COMMANDS--
prline + Guess the next number will be positive
prline 0 Guess the next number will be ground (zero)
prline - Guess the next number will be negative
prline h Print this help text.
prline a about Guess That Trit
prline q quit Guess That Trit (and get final score ;) )
prline 
return
