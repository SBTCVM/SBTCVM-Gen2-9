val @70
set cp.max
asm head-nspout=1

val @0
set cp.clearbuffer

var cmdflg=0
var validflg=0

gsub bigsplash
gsub splash
textcolor *++0---
prline  v0.1 [PROTOTYPE]
prline Type 'help' for help.
textcolor *0+0---

label main
	textcolor *0+0---
	print 0/>
	textcolor *+++---
	gsub cp.getentry
	
	newline
	val 0
	set cmdflg
	set validflg
	tabstrc cp.buffer,@0,@0,help
		set cmdflg
		if cmdflg,@1 gsub help
		if cmdflg,@1 =validflg @1
	tabstrc cp.buffer,@0,@0,about
		set cmdflg
		if cmdflg,@1 gsub about
		if cmdflg,@1 =validflg @1
	tabstrc cp.buffer,@0,@0,vdi
		set cmdflg
		if cmdflg,@1 gsub vdi
		if cmdflg,@1 =validflg @1
	tabstrc cp.buffer,@0,@0,shutdown
		set cmdflg
		if cmdflg,@1 gsub shutdown
		if cmdflg,@1 =validflg @1
	if validflg,@0 gsub invalid
goto main

label invalid
textcolor *++0---
prline Unknown Command. Nether Builtin nor executable found!
return

var vdi_vdioutput_buffer=0

label vdi
	val @1
	asm iowrite1;>vdi.cli.status
	uiter vdi_iter,vdi_buffiter,@4,cp.itersize
	val :\n
	asm iowrite1;>vdi.cli.in
	label vdi_outloop
		asm ioread1;>vdi.cli.out
		set vdi_vdioutput_buffer
		
		if vdi_vdioutput_buffer,@0 return
		chardump vdi_vdioutput_buffer
	goto vdi_outloop


label vdi_buffiter
	tabr cp.buffer,vdi_iter,@0
	asm iowrite1;>vdi.cli.in
return

label help
textcolor *+++--0
prline vdi [cmd] : Pass commands directly to SBTVDI Serial Console. see: 'vdi help'
prline help      : This text.
print shutdown  : Shutdown SBTCVM-DOS
textcolor *0+0---
newline
return

label about
textcolor *+++--0
newline
prline --ABOUT--
gsub splash
textcolor *+++--0
prline  v0.1 (SBTCVM Disk Operating System)
prline Part of SBTCVM Gen2-9
prline 
prline Copyright (c) 2018-2019 Thomas Leathers & Contributors
prline 
prline SBTCVM-DOS is free software: you can redistribute it and/or modify
prline it under the terms of the GNU General Public License as published by
prline the Free Software Foundation, either version 3 of the License, or
prline (at your option) any later version.
prline 
prline SBTCVM-DOS is distributed in the hope that it will be useful,
prline but WITHOUT ANY WARRANTY; without even the implied warranty of
prline MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
prline GNU General Public License for more details.
prline 
prline You should have received a copy of the GNU General Public License
print along with SBTCVM-DOS. If not, see <http://www.gnu.org/licenses/>

#ensure last output line is a 'print' statement, not a 'prline' statement!

textcolor *0+0---
newline
return

label splash
	textcolor *+++0--
	print S
	textcolor *+++00-
	print B
	textcolor *+++-0-
	print T
	textcolor *+++-00
	print C
	textcolor *+++--0
	print V
	textcolor *+++0-0
	print M
	textcolor *+++---
	print -
	textcolor *0++---
	print DOS
	textcolor *+++---
return

label bigsplash
asm include;dos+bigsplash.tas0
return

label shutdown
gsub splash
prline  is shutting down...
prline done.
stop




include comprompt as cp
