############### PLRLE DECODER: image location variable.
var offset=0

#pixel sizes
var pixx=@1
var pixy=@1
#start position
var posx=@-121
var posy=@-121

#note: this version of PLRLE has the transparancy key support removed
#for speed concerns.

############# SBTCVM GFXCON PLRLE COMPRESSION DECODER #############
#Code By: Thomas Leathers
#
#

var RL_pointer=0
var RL_SIZE=0
var RL_loopcount=0
var RL_data=0
var draw_pixx=0
var draw_pixy=0
var px=0
var py=0

label decode
	get posx
	set px
	get posy
	set py
	
	drawy1 posy
	drawx1 posx
	
	get1 pixx
	asm datawrite1;>PIX_X_HACK
	
	get1 pixy
	asm datawrite1;>PIX_Y_HACK
	
	add pixx,@1
	set draw_pixx
	add pixy,@1
	set draw_pixy
	
	drawwidth draw_pixx
	drawheight draw_pixy
	
	val 0
	set RL_loopcount
	copy offset,RL_pointer
	
	get RL_pointer
	asm datawrite1;>RL_GET_SIZE
	asm dataread1;;RL_GET_SIZE
	set RL_SIZE
	sub RL_SIZE,@1
	set RL_SIZE
	
	add RL_pointer,@1
	set RL_pointer
	
	#iterate decoder loop over data
	uiter rl_iter,RL_decode_mainloop,RL_pointer,RL_SIZE
	

return

####get new datavalue####
label RL_decode_mainloop
	
	#set pointers
	get rl_iter
	asm datawrite1;>RL_GET_DATA
	asm datawrite1;>RL_GET_LEN
	
	#get length of new datavalue
	
	
	# get new datavalue
	asm dataread1;;RL_GET_DATA
	asm iowrite1;>plot.color
	
	
	#newline code
	asm instread1;;RL_GET_LEN
	
	asm setreg2;10x-1
	asm gotoif;>RL_NEWLINE
	
	###Rectangle plot cod
		asm adddata1;10x1
		#get2 pixx
		#asm mul
		asm muldata1;10x0;PIX_X_HACK
		
			asm adddata1;10x1
			#faster
			asm iowrite1;>plot.width
			asm subdata1;10x1
			
			asm iowrite1;>plot.rect
		get2 px
		asm add2
		set2 px
		asm iowrite2;>plot.x1
	
	return

asm zerosize;;RL_NEWLINE
	get1 py
	asm adddata1;10x0;PIX_Y_HACK
	set1 py
	
	asm iowrite1;>plot.y1
	get posx
	set px
	
	asm iowrite1;>plot.x1
return



################END OF PLRLE DECODER ROUTINES#########
