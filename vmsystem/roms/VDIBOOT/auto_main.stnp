
#initial splash text


gsub BootUp

var kb=0

label MAIN
		prline please choose option:
			textcolor *000000
			print _______________________________________
			textcolor *+++---
			newline
		prline   [b] - Try to bootup again
		prline   [1] - SBTVDI SERIAL Wrapper shell
		prline   [2] - Quick test menu.
		prline   [q] - shutdown/QUIT SBTCVM Session
		print >
	keyprompt
		set kb
		newline
		if kb,:1 gsub vdi_serial
		if kb,:2 gsub qtest.MAIN
		if kb,:q stop
		if kb,:Q stop
		if kb,:b gsub BootUp
		if kb,:B gsub BootUp
goto MAIN

label BootUp
	gsub splash
		vdimode @1
		print 0...
		vdi rstld 0 boot.txe
		print 1...
		vdi rstld 1 boot.txe
		newline
	textcolor *+0----
	print unsuccessful... 
	textcolor *+++---
return

label splash
	textcolor *++0---
	prline  ^^^^^^^^^^^^^^^
	prline <+-------------+>
	prline <|      x      |>
	prline <| .___/0\___. |>  SBTCVM Gen 2-9
	prline <| |+/     \-| |>  --------------
	prline <| |/  <|>  \| |>  SBTCVM-SBTVDI BIOS v0.4
	prline <| |<|> _ <|>| |>
	prline <| \  ._|_.  / |>
	prline <|__|       |__|>  Begin drive bootup...
	prline <+Mni-------inM+>
	print  vvvvvvvvvvvvvvv   
	textcolor *+++---
return



label vdi_serial
	
	
	textcolor *0++--0
	
	prline booting into SBTVDI Shell...
	
	
	textcolor *+++---
	
	var exitcode=0
	
	#simple VDI shell in SSTNPL with vdishell.tas0 (part of standard library)
	asm include;vdishell.tas0
	asm dataread1;>vdiexitcode
	set exitcode
	
	
	prline Shell has returned...
	dumpd exitcode
	newline
	if exitcode,@1 return
	if exitcode,@2 stop
return

include VDIBOOT/quicktest as qtest